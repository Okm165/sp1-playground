
/// @title SP1 Verifier
/// @author Succinct Labs
/// @notice This contracts implements a solidity verifier for SP1.
contract SP1Verifier is Verifier {
    /// @notice Deserializes a proof from the given bytes.
    /// @param proofBytes The proof bytes.
    function deserializeProof(
        bytes calldata proofBytes
    )
        public
        pure
        returns (
            uint256[8] calldata proof,
            uint256[2] calldata commitments,
            uint256[2] calldata commitmentPok
        )
    {
        require(
            proofBytes.length == 8 * 32 + 4 + 2 * 32 + 2 * 32,
            "invalid proof bytes length"
        );
        
        // Map the calldata pointers.
        assembly {
            proof := proofBytes.offset
            commitments := add(add(mul(8, 0x20), 0x4), proofBytes.offset)
            commitmentPok := add(mul(2, 0x20), commitments)
        }
    }

    /// @notice Hashes the public values to a field elements inside Bn254.
    /// @param publicValues The public values.
    function hashPublicValues(
        bytes calldata publicValues
    ) public pure returns (bytes32) {
        return sha256(publicValues) & bytes32(uint256((1 << 253) - 1));
    }

    /// @notice Verifies a proof with given public values and vkey.
    /// @param vkey The verification key for the RISC-V program.
    /// @param publicValues The public values encoded as bytes.
    /// @param proofBytes The proof of the program execution the SP1 zkVM encoded as bytes.
    function verifyProof(
        bytes32 vkey, 
        bytes calldata publicValues,
        bytes calldata proofBytes
    ) public view {
        (
            uint256[8] calldata proof,
            uint256[2] calldata commitments,
            uint256[2] calldata commitmentPok
        ) = deserializeProof(proofBytes);
        bytes32 publicValuesDigest = hashPublicValues(publicValues);
        uint256[2] memory inputs = [
            uint256(vkey),
            uint256(publicValuesDigest)
        ];
        this.verifyProof(proof, commitments, commitmentPok, inputs);
    }
}
